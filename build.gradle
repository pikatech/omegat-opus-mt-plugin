plugins {
    id 'java'
    id 'checkstyle'
    id 'distribution'
    id 'maven'
    id 'idea'
	id 'eclipse'
}

repositories {
    mavenCentral()
}

def compileDependencies = [
        'org.omegat:omegat:5.7.1',
        'commons-io:commons-io:2.7',
        'commons-lang:commons-lang:2.6'
]

def testDependencies = [
        'junit:junit:4.13.2',
        'org.xmlunit:xmlunit-legacy:2.8.2',
        'org.madlonkay:supertmxmerge:2.0.3'
]

dependencies {

    compileDependencies.each {
        compileOnly it
        //testImplementation it
    }

    testDependencies.each {
        //testImplementation it
    }
}


tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

jar {
    manifest {
        // plugin's main class name is defined in gradle.properties file.
        attributes(
                "OmegaT-Plugins": pluginMainClass,
                "Plugin-Version": version,
                "Plugin-Name": pluginName,
                "Main-Class": pluginMainClass
        )
    }
}

/* Checkstyle */
checkstyle {
    config = resources.text.fromFile("config/checkstyle/checkstyle.xml")
    ignoreFailures = true
    toolVersion = '8.41'
    sourceSets = [project.sourceSets.main]
}
tasks.checkstyleMain.setGroup('Verification')
tasks.remove(tasks['checkstyleTest'])

artifacts {
    archives jar
}

distTar {
    compression = Compression.GZIP
}

distributions {
    main {
        contents {
            from('README.md', 'COPYING')
            into('docs') {
                from 'CHANGELOG.md'
            }

            from(jar) {
                into 'plugin'
            }
        }
    }
}


task installPlugin(type: Copy) {
        from 'build/install/omegat-plugin-fake-mt/plugin'
        into omegatPluginDir
        include('*.jar')
}

installPlugin.dependsOn installDist
